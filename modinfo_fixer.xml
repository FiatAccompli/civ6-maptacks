<?xml version="1.0"?>
<xsl:stylesheet version="1.0" xmlns:xsl="http://www.w3.org/1999/XSL/Transform">
  <xsl:output method="xml" encoding="utf-8" indent="yes"/>
  <xsl:strip-space elements="*"/>
  
  <xsl:template match="/Mod/Properties">
    <xsl:copy>
      <xsl:apply-templates select="@*|node()"/>
    </xsl:copy>
    <LocalizedText>
      <Text id="LOC_MAP_TACKS_MOD_NAME">
        <en_US>Map Tacks +</en_US>
        <de_DE>Map Tacks +</de_DE>
        <es_ES>Map Tacks +</es_ES>
        <fr_FR>Map Tacks +</fr_FR>
        <it_IT>Map Tacks +</it_IT>
        <pl_PL>Map Tacks +</pl_PL>
        <pt_BR>Map Tacks +</pt_BR>
        <ru_RU>Map Tacks +</ru_RU>
        <ja_JP>Map Tacks +</ja_JP>
        <ko_KR>Map Tacks + </ko_KR>
        <zh_Hans_CN>Map Tack +</zh_Hans_CN>
        <zh_Hant_HK>Map Tacks +</zh_Hant_HK>
      </Text>
      <Text id="LOC_MAP_TACKS_TEASER">
        <en_US>Improve the user interface for map pins.</en_US>
        <de_DE>Verbessere die Benutzerschnittstelle für Landkartennadeln.</de_DE>
        <es_ES>Mejore la interfaz de usuario para los marcadores del mapa.</es_ES>
        <fr_FR>Améliorer l'interface utilisateur pour les marqueurs.</fr_FR>
        <it_IT>Migliora l'interfaccia utente per i spilli mappa.</it_IT>
        <pl_PL>Ulepsz interfejs użytkownika dla znaczniki na mapie.</pl_PL>
        <pt_BR>Melhore a interface do usuário para os marcadores de mapa.</pt_BR>
        <ru_RU>Улучшите интерфейс пользователя для отметки на карте.</ru_RU>
        <!-- borrow the in-game localization for map pins -->
        <ja_JP>LOC_HUD_MAP_PIN_LIST</ja_JP>
        <ko_KR>LOC_HUD_MAP_PIN_LIST</ko_KR>
        <zh_Hans_CN>LOC_HUD_MAP_PIN_LIST</zh_Hans_CN>
        <zh_Hant_HK>LOC_HUD_MAP_PIN_LIST</zh_Hant_HK>
      </Text>
    </LocalizedText>
  </xsl:template>

  <!-- Inserted by the above block, so remove it if it's already there (i.e. this transform is applied twice). -->
  <xsl:template match="/Mod/LocalizedText"/>

  <!-- Modbuddy writes priority (lowercase) attribute, but the game looks for Priority (uppercase).  
       Because I guess even the most cursory testing is unnecessary for Firaxis. -->
  <xsl:template match="@priority">
    <xsl:attribute name="Priority">
      <xsl:value-of select="."/>
    </xsl:attribute>
  </xsl:template>

  <!--Identity template, provides default behavior that copies all content into the output -->
  <xsl:template match="@*|node()">
    <xsl:copy>
      <xsl:apply-templates select="@*|node()"/>
    </xsl:copy>
  </xsl:template>
</xsl:stylesheet>